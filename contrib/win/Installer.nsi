; @brief Installer script for Windows
; @author Nils Durner

; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "GNUnet"
!define PRODUCT_VERSION "0.6.6"
!define PRODUCT_PUBLISHER "GNU"
!define PRODUCT_WEB_SITE "http://www.ovmj.org/GNUnet/"
;!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\gnunet-gtk.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"

SetCompressor lzma

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\nsis1-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\nsis1-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "License.rtf"
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "GNUnet"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Configuration file
Page custom InitStep1
Page custom InitStep2 DoneStep2
; Finish page
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\README"
!define MUI_FINISHPAGE_SHOWREADME_FUNCTION "ShowReadme"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "German"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Setup.exe"
InstallDir "$PROGRAMFILES\GNU\GNUnet"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Var LANGCODE
Var STEP2_INI
Var GNUNET_INI

Function InitStep1
  Push $R0
  Push $R1
  Push $R2

  InstallOptions::initDialog /NOUNLOAD "$PLUGINSDIR\config_$LANGCODE_step1.ini"
  Pop $R0

  GetDlgItem $R1 $R0 1200 ;1200 + Field number - 1

  ;$R1 contains the HWND of the first field
  CreateFont $R2 "Tahoma" 14 700
  SendMessage $R1 ${WM_SETFONT} $R2 0

  InstallOptions::show
  Pop $R0

  Pop $R2
  Pop $R1
  Pop $R0
FunctionEnd

Function InitStep2
  StrCpy $STEP2_INI "$PLUGINSDIR\config_$LANGCODE_step2.ini"
  StrCpy $GNUNET_INI "$INSTDIR\etc\gnunet.conf"

  ReadINIStr $R0 $GNUNET_INI "LOAD" "MAXNETUPBPSTOTAL"
  WriteINIStr $STEP2_INI "Field 4" "State" $R0
  ReadINIStr $R0 $GNUNET_INI "LOAD" "MAXNETDOWNBPSTOTAL"
  WriteINIStr $STEP2_INI "Field 6" "State" $R0
  ReadINIStr $R0 $GNUNET_INI "LOAD" "BASICLIMITING"
  StrCmp $R0 "YES" 0 adv_limit
  WriteINIStr $STEP2_INI "Field 8" "State" "1"
  WriteINIStr $STEP2_INI "Field 9" "State" "0"
  goto ip
 adv_limit:
  WriteINIStr $STEP2_INI "Field 8" "State" "0"
  WriteINIStr $STEP2_INI "Field 9" "State" "1"
 ip:
  ReadINIStr $R0 $GNUNET_INI "NETWORK" "IP"
  WriteINIStr $STEP2_INI "Field 11" "State" $R0
  ReadINIStr $R0 $GNUNET_INI "LOAD" "MAXCPULOAD"
  WriteINIStr $STEP2_INI "Field 14" "State" $R0
  ReadINIStr $R0 $GNUNET_INI "AFS" "ACTIVEMIGRATION"
  StrCmp $R0 "YES" 0 no_mig
  WriteINIStr $STEP2_INI "Field 15" "State" "1"
  goto dialog
 no_mig:
  WriteINIStr $STEP2_INI "Field 15" "State" "0"
 dialog:
  FlushINI $STEP2_INI
 
  Push $R0
  Push $R1
  Push $R2

  InstallOptions::initDialog /NOUNLOAD $STEP2_INI
  Pop $R0

  GetDlgItem $R1 $R0 1215 ;1200 + Field number - 1

  SetOutPath "$INSTDIR\bin"
  ;  Initialize DLL
  System::Get "$INSTDIR\bin\libgnunetutil-0.dll::InitWinEnv() ? c"
  Pop $0
  System::Call "$0"

  ;$R1 contains the handle of the NIC combo
  System::Get "$INSTDIR\bin\libgnunetutil-0.dll::PopulateNICCombo(i $R1) ? c"
  Pop $0
  System::Call "$0"

  ;  Unitialize DLL
  System::Get "$INSTDIR\bin\libgnunetutil-0.dll::ShutdownWinEnv() ? c"
  Pop $0
  System::Call "$0"

  InstallOptions::show
  Pop $R0

  Pop $R2
  Pop $R1
  Pop $R0
FunctionEnd

Function DoneStep2
  ReadINIStr $R0 $STEP2_INI "Field 4" "State"
  WriteINIStr $GNUNET_INI "LOAD" "MAXNETUPBPSTOTAL" $R0
  ReadINIStr $R0 $STEP2_INI "Field 6" "State"
  WriteINIStr $GNUNET_INI "LOAD" "MAXNETDOWNBPSTOTAL" $R0
  ReadINIStr $R0 $STEP2_INI "Field 8" "State"
  StrCmp $R0 "1" 0 adv_limit
  WriteINIStr $GNUNET_INI "LOAD" "BASICLIMITING" "YES"
  goto nw
 adv_limit:
  WriteINIStr $GNUNET_INI "LOAD" "BASICLIMITING" "NO"
 nw:
  ReadINIStr $R0 $STEP2_INI "Field 11" "State"
  WriteINIStr $GNUNET_INI "NETWORK" "IP" $R0
  ReadINIStr $R0 $STEP2_INI "Field 14" "State"
  WriteINIStr $GNUNET_INI "LOAD" "MAXCPULOAD" $R0
  ReadINIStr $R0 $STEP2_INI "Field 15" "State"
  StrCmp $R0 "1" 0 no_mig
  WriteINIStr $GNUNET_INI "AFS" "ACTIVEMIGRATION" "YES"
  goto startup
 no_mig:
  WriteINIStr $GNUNET_INI "AFS" "ACTIVEMIGRATION" "NO"
 startup:
  ReadINIStr $R0 $STEP2_INI "Field 1" "State"
  SetOutPath "$INSTDIR\bin"
  StrCmp $R0 "1" 0 no_start
  ;Check for NT
  ClearErrors
  ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
  IfErrors startup_win9
  ExecShell "" "gnunet-win-tool.exe" "-i" SW_SHOWMINIMIZED
  goto db
 startup_win9:
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Run" "GNUnet" "$INSTDIR\bin\gnunetd.exe"
  goto db
 no_start:
  ;Check for NT
  ClearErrors
  ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
  IfErrors no_startup_win9
  ExecShell "" "gnunet-win-tool.exe" "-u" SW_SHOWMINIMIZED
 no_startup_win9:
  DeleteRegValue HKLM "Software\Microsoft\Windows\CurrentVersion\Run" "GNUnet"
 db:
  WriteINIStr $GNUNET_INI "AFS" "DATABASETYPE" '"sqlite"'
  ;Get NIC string
  ReadINIStr $R0 $STEP2_INI "Field 16" "State"
  ; load length into $R2
  StrLen $R2 $R0
  ; init counter $R1
  StrCpy $R1 "0"
  rd_idx:
  ; inc counter $R1
  IntOp $R1 $R1 + 1
  ; we want to examine the last $R1 chars
  IntOp $R4 $R2 - $R1
  ; get the first char of the substring
  StrCpy $R3 $R0 1 $R4
  ; is it a space? yes: we have found the beginning of the NIC index
  StrCmp $R3 " " 0 rd_idx
  ; copy NIC index to $R3
  IntOp $R4 $R4 + 1
  IntOp $R1 $R1 - 1
  StrCpy $R3 $R0 $R1 $R4

  WriteINIStr $GNUNET_INI "NETWORK" "INTERFACE" '$R3'
  WriteINIStr $GNUNET_INI "LOAD" "INTERFACES" '$R3'
FunctionEnd

Var USR_PROF
Var DIRLEN

Section "Core files" SEC01
  SetOutPath "$INSTDIR\bin"
  SetOverwrite ifnewer
  File "C:\GNUnet\bin\gnunet-gtk.exe"
  File "gnu.ico"
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\GNUnet.lnk" "$INSTDIR\bin\gnunet-gtk.exe" "" "$INSTDIR\bin\gnu.ico"
  CreateShortCut "$DESKTOP\GNUnet.lnk" "$INSTDIR\bin\gnunet-gtk.exe" "" "$INSTDIR\bin\gnu.ico"
; Microsoft C Runtime
; Redistribution is permitted, see Microsoft Knowledge Base Article 326922
  File "C:\GNUnet\bin\msvcr70.dll"
  File "C:\GNUnet\bin\libsqlite3-0.dll"
  File "C:\GNUnet\bin\pthreadGC.dll"
  File "C:\GNUnet\bin\libz.dll"
  File "C:\GNUnet\bin\libpng12.dll"
  File "C:\GNUnet\bin\libpango-1.0-0.dll"
  File "C:\GNUnet\bin\libpangowin32-1.0-0.dll"
  File "C:\GNUnet\bin\libltdl-3.dll"
  File "C:\GNUnet\bin\libintl.dll"
  File "C:\GNUnet\bin\libgtk-win32-2.0-0.dll"
  File "C:\GNUnet\bin\libgtk-0.dll"
  File "C:\GNUnet\bin\libgthread-2.0-0.dll"
  File "C:\GNUnet\bin\libgobject-2.0-0.dll"
  File "C:\GNUnet\bin\libgmp.dll"
  File "C:\GNUnet\bin\libgnunetutil-0.dll"
  File "C:\GNUnet\bin\libgnunettransport_udp.dll"
  File "C:\GNUnet\bin\libgnunettransport_tcp.dll"
  File "C:\GNUnet\bin\libgnunettransport_nat.dll"
  File "C:\GNUnet\bin\libgnunettransport_http.dll"
  File "C:\GNUnet\bin\libgnunettracekit_protocol.dll"
  File "C:\GNUnet\bin\libgnunettbench_protocol.dll"
  File "C:\GNUnet\bin\libgnunetchat_protocol.dll"
  File "C:\GNUnet\bin\libgnunetafs_protocol.dll"
  File "C:\GNUnet\bin\libgnunetafs_database_sqlite.dll"
  File "C:\GNUnet\bin\libgnunetafs_database_directory.dll"
  File "C:\GNUnet\bin\libgnunetafs_database_bdb.dll"
  File "C:\GNUnet\bin\libgnunet_afs_esed2-0.dll"
  File "C:\GNUnet\bin\libgmodule-2.0-0.dll"
  File "C:\GNUnet\bin\libglib-2.0-0.dll"
  File "C:\GNUnet\bin\libgdk_pixbuf-2.0-0.dll"
  File "C:\GNUnet\bin\libgdk-win32-2.0-0.dll"
  File "C:\GNUnet\bin\libgdk-0.dll"
  File "C:\GNUnet\bin\libdb.dll"  
  File "C:\GNUnet\bin\libatk-1.0-0.dll"
  File "C:\GNUnet\bin\intl.dll"
  File "C:\GNUnet\bin\iconv.dll"
  File "C:\GNUnet\bin\libiconv-2.dll"
  File "C:\GNUnet\bin\gnunet-win-tool.exe"
  File "C:\GNUnet\bin\gnunet-transport-check.exe"
  File "C:\GNUnet\bin\gnunet-tracekit.exe"
  File "C:\GNUnet\bin\gnunet-tbench.exe"
  File "C:\GNUnet\bin\gnunet-update.exe"
  File "C:\GNUnet\bin\gnunet-stats.exe"
  File "C:\GNUnet\bin\gnunet-setup.exe"
  File "C:\GNUnet\bin\gnunet-search.exe"
  File "C:\GNUnet\bin\gnunet-pseudonym.exe"
  File "C:\GNUnet\bin\gnunet-peer-info.exe"
  File "C:\GNUnet\bin\gnunet-insert.exe"
  File "C:\GNUnet\bin\gnunet-download.exe"
  File "C:\GNUnet\bin\gnunet-directory.exe"
  File "C:\GNUnet\bin\gnunet-delete.exe"
  File "C:\GNUnet\bin\gnunetd.exe"
  File "C:\GNUnet\bin\gnunet-convert.exe"
  File "C:\GNUnet\bin\gnunet-check.exe"
  File "C:\GNUnet\bin\gnunet-chat.exe"
  SetOutPath "$INSTDIR\"
  File "C:\GNUnet\README"
  File "C:\GNUnet\PLATFORMS"
  File "C:\GNUnet\ChangeLog"
  File "C:\GNUnet\COPYING"
  File "C:\GNUnet\AUTHORS"
  File "C:\GNUnet\UPDATING"
  SetOutPath "$INSTDIR\etc\pango"
  File "C:\GNUnet\etc\pango\pango.aliases"
  File "C:\GNUnet\etc\pango\pango.modules"
  SetOutPath "$INSTDIR\lib\gtk-2.0\2.4.0\engines"
  File "C:\GNUnet\lib\gtk-2.0\2.4.0\engines\libwimp.dll"
  SetOutPath "$INSTDIR\lib\pango\1.4.0\modules"
  File "C:\GNUnet\lib\pango\1.4.0\modules\pango-basic-win32.dll"
  SetOutPath "$INSTDIR\share\themes\Default\gtk-2.0"
  File "C:\GNUnet\share\themes\Default\gtk-2.0\gtkrc.gtkwimp"
  File "C:\GNUnet\share\themes\Default\gtk-2.0\gtkrc.plain"  
  CopyFiles "gtkrc.gtkwimp" "gtkrc"
  SetOutPath "$INSTDIR\tmp"
SectionEnd

Section "Configuration files" SEC02
  ReadEnvStr $USR_PROF "USERPROFILE"
  StrLen $DIRLEN $USR_PROF
  IntCmp $DIRLEN 0 no_profile
  goto cp_conf
 no_profile:
  ReadRegStr $USR_PROF "HKCU" "Software\Microsoft\Windows\CurrentVersion\Explorer\Shell Folders" "Personal"
  StrLen $DIRLEN $USR_PROF
  IntCmp $DIRLEN 0 gn_dir
  goto cp_conf
 gn_dir:
  StrCpy $USR_PROF "$INSTDIR\home"
 cp_conf:
  StrCpy $USR_PROF "$USR_PROF\.gnunet\"
  SetOutPath $USR_PROF
  File "C:\GNUnet\etc\gnunet.user"
  SetOutPath "$INSTDIR\etc\"
  File "C:\GNUnet\etc\gnunet.root"
  Rename "$INSTDIR\etc\gnunet.root" "$INSTDIR\etc\gnunet.conf"
  Rename "$USR_PROF\gnunet.user" "$USR_PROF\gnunet.conf"
SectionEnd

Section "Extractor" SEC03
  SetOutPath "$INSTDIR\bin"
  SetOverwrite ifnewer
  File "c:\GNUnet\bin\extract.exe"
  File "c:\GNUnet\bin\libextractor-1.dll"
  File "c:\GNUnet\bin\libextractor_asf.dll"
  File "c:\GNUnet\bin\libextractor_deb.dll"
  File "c:\GNUnet\bin\libextractor_dvi.dll"
  File "c:\GNUnet\bin\libextractor_elf.dll"
  File "c:\GNUnet\bin\libextractor_filename.dll"
  File "c:\GNUnet\bin\libextractor_gif.dll"
  File "c:\GNUnet\bin\libextractor_hash_md5.dll"
  File "c:\GNUnet\bin\libextractor_hash_rmd160.dll"
  File "c:\GNUnet\bin\libextractor_hash_sha1.dll"
  File "c:\GNUnet\bin\libextractor_html.dll"
  File "c:\GNUnet\bin\libextractor_id3v2.dll"
  File "c:\GNUnet\bin\libextractor_id3v23.dll"
  File "c:\GNUnet\bin\libextractor_id3v24.dll"
  File "c:\GNUnet\bin\libextractor_jpeg.dll"
  File "c:\GNUnet\bin\libextractor_lower.dll"
  File "c:\GNUnet\bin\libextractor_man.dll"
  File "c:\GNUnet\bin\libextractor_mime.dll"
  File "c:\GNUnet\bin\libextractor_mp3.dll"
  File "c:\GNUnet\bin\libextractor_mpeg.dll"
  File "c:\GNUnet\bin\libextractor_ole2.dll"
  File "c:\GNUnet\bin\libextractor_oo.dll"
  File "c:\GNUnet\bin\libextractor_pdf.dll"
  File "c:\GNUnet\bin\libextractor_png.dll"
  File "c:\GNUnet\bin\libextractor_printable_da.dll"
  File "c:\GNUnet\bin\libextractor_printable_de.dll"
  File "c:\GNUnet\bin\libextractor_printable_en.dll"
  File "c:\GNUnet\bin\libextractor_printable_es.dll"
  File "c:\GNUnet\bin\libextractor_printable_it.dll"
  File "c:\GNUnet\bin\libextractor_printable_no.dll"
  File "c:\GNUnet\bin\libextractor_ps.dll"
  File "c:\GNUnet\bin\libextractor_qt.dll"
  File "c:\GNUnet\bin\libextractor_real.dll"
  File "c:\GNUnet\bin\libextractor_riff.dll"
  File "c:\GNUnet\bin\libextractor_rpm.dll"
  File "c:\GNUnet\bin\libextractor_split.dll"
  File "c:\GNUnet\bin\libextractor_tar.dll"
  File "c:\GNUnet\bin\libextractor_tiff.dll"
  File "c:\GNUnet\bin\libextractor_wav.dll"
  File "c:\GNUnet\bin\libextractor_zip.dll"
SectionEnd

Section "DHT" Sec04
  File "c:\GNUnet\bin\gnunet-dht-join.exe"
  File "c:\GNUnet\bin\gnunet-dht-query.exe"
  File "c:\GNUnet\bin\libgnunetdht_api-0.dll"
  File "c:\GNUnet\bin\libgnunetdht_datastore_memory-0.dll"
  File "c:\GNUnet\bin\libgnunetdht_protocol.dll"
  File "c:\GNUnet\bin\libgnunetrpc_protocol.dll"
  File "c:\GNUnet\bin\libgnunetrpc_util-0.dll"
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
;  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\bin\gnunet-gtk.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\bin\gnu.ico"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "${PRODUCT_STARTMENU_REGVAL}" "$ICONS_GROUP"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  WriteRegStr HKLM "Software\GNU\GNUnet" "InstallDir" "$INSTDIR"
  WriteRegStr HKLM "Software\GNU\libextractor" "InstallDir" "$INSTDIR"
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "Required by GNUnet"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "GNUnet configuration files"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC03} "Component to extract meta-data from files"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC04} "Distributed HashTables. Experimental."
!insertmacro MUI_FUNCTION_DESCRIPTION_END


Function .onInit
  InitPluginsDir
  
  StrCmp $LANGUAGE "1031" 0 english
  StrCpy $LANGCODE "de"
  goto cp
 english:
  StrCpy $LANGCODE "en"

 cp:
  File /oname=$PLUGINSDIR\config_en_step1.ini config_en_step1.ini
  File /oname=$PLUGINSDIR\config_en_step2.ini config_en_step2.ini
  File /oname=$PLUGINSDIR\config_de_step1.ini config_de_step1.ini
  File /oname=$PLUGINSDIR\config_de_step2.ini config_de_step2.ini
FunctionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully deinstalled."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Do you want to deinstall $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Function ShowReadme
  Exec "notepad $INSTDIR\README"
FunctionEnd

Section Uninstall
  ReadRegStr $ICONS_GROUP ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "${PRODUCT_STARTMENU_REGVAL}"
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\etc\gnunet.conf"
  ExpandEnvStrings $USR_PROF "%USERPROFILE%\.gnunet\gnunet.conf"
  Delete $USR_PROF
  Delete "$INSTDIR\UPDATING"
  Delete "$INSTDIR\AUTHORS"
  Delete "$INSTDIR\ChangeLog"
  Delete "$INSTDIR\COPYING"
  Delete "$INSTDIR\PLATFORMS"
  Delete "$INSTDIR\README"
  Delete "$INSTDIR\bin\gnunet-chat.exe"
  Delete "$INSTDIR\bin\gnunet-check.exe"
  Delete "$INSTDIR\bin\gnunet-convert.exe"
  Delete "$INSTDIR\bin\gnunetd.exe"
  Delete "$INSTDIR\bin\gnunet-delete.exe"
  Delete "$INSTDIR\bin\gnunet-directory.exe"
  Delete "$INSTDIR\bin\gnunet-directory-emptydb.exe"
  Delete "$INSTDIR\bin\gnunet-directory-listdb.exe"
  Delete "$INSTDIR\bin\gnunet-directory-print.exe"
  Delete "$INSTDIR\bin\gnunet-download.exe"
  Delete "$INSTDIR\bin\gnunet-insert.exe"
  Delete "$INSTDIR\bin\gnunet-peer-info.exe"
  Delete "$INSTDIR\bin\gnunet-pseudonym.exe"
  Delete "$INSTDIR\bin\gnunet-pseudonym-create.exe"
  Delete "$INSTDIR\bin\gnunet-pseudonym-delete.exe"
  Delete "$INSTDIR\bin\gnunet-pseudonym-list.exe"
  Delete "$INSTDIR\bin\gnunet-search.exe"
  Delete "$INSTDIR\bin\gnunet-search-sblock.exe"
  Delete "$INSTDIR\bin\gnunet-setup.exe"
  Delete "$INSTDIR\bin\gnunet-stats.exe"
  Delete "$INSTDIR\bin\gnunet-tbench.exe"
  Delete "$INSTDIR\bin\gnunet-testbed.exe"
  Delete "$INSTDIR\bin\gnunet-tracekit.exe"
  Delete "$INSTDIR\bin\gnunet-transport-check.exe"
  Delete "$INSTDIR\bin\gnunet-update.exe"
  Delete "$INSTDIR\bin\gnunet-win-tool.exe"
  Delete "$INSTDIR\bin\iconv.dll"
  Delete "$INSTDIR\bin\libiconv-2.dll"
  Delete "$INSTDIR\bin\intl.dll"
  Delete "$INSTDIR\bin\libatk-1.0-0.dll"
  Delete "$INSTDIR\bin\libcharset.dll"
  Delete "$INSTDIR\bin\libdb.dll"
  Delete "$INSTDIR\bin\libeay32-0.dll"
  Delete "$INSTDIR\bin\libgdbm-2.dll"
  Delete "$INSTDIR\bin\libgdk-0.dll"
  Delete "$INSTDIR\bin\libgdk_pixbuf-2.0-0.dll"
  Delete "$INSTDIR\bin\libgdk-win32-2.0-0.dll"
  Delete "$INSTDIR\bin\libglib-2.0-0.dll"
  Delete "$INSTDIR\bin\libgmodule-2.0-0.dll"
  Delete "$INSTDIR\bin\libgnunet_afs_esed2-0.dll"
  Delete "$INSTDIR\bin\libgnunetafs_database_bdb.dll"
  Delete "$INSTDIR\bin\libgnunetafs_database_directory.dll"
  Delete "$INSTDIR\bin\libgnunetafs_database_gdbm.dll"
  Delete "$INSTDIR\bin\libgnunetafs_database_sqlite.dll"
  Delete "$INSTDIR\bin\libgnunetafs_protocol.dll"
  Delete "$INSTDIR\bin\libgnunetchat_protocol.dll"
  Delete "$INSTDIR\bin\libgnunettbench_protocol.dll"
  Delete "$INSTDIR\bin\libgnunettestbed_protocol.dll"
  Delete "$INSTDIR\bin\libgnunettracekit_protocol.dll"
  Delete "$INSTDIR\bin\libgnunettransport_http.dll"
  Delete "$INSTDIR\bin\libgnunettransport_nat.dll"
  Delete "$INSTDIR\bin\libgnunettransport_tcp.dll"
  Delete "$INSTDIR\bin\libgnunettransport_udp.dll"
  Delete "$INSTDIR\bin\libgnunetutil-0.dll"
  Delete "$INSTDIR\bin\libgmp.dll"  
  Delete "$INSTDIR\bin\libgobject-2.0-0.dll"
  Delete "$INSTDIR\bin\libgthread-2.0-0.dll"
  Delete "$INSTDIR\bin\libgtk-0.dll"
  Delete "$INSTDIR\bin\libgtk-win32-2.0-0.dll"
  Delete "$INSTDIR\bin\libintl.dll"
  Delete "$INSTDIR\bin\libltdl-3.dll"
  Delete "$INSTDIR\bin\libpango-1.0-0.dll"
  Delete "$INSTDIR\bin\libpangowin32-1.0-0.dll"
  Delete "$INSTDIR\bin\libpng12.dll"
  Delete "$INSTDIR\bin\libz.dll"
  Delete "$INSTDIR\bin\pango-basic-fc.dll"
  Delete "$INSTDIR\bin\pango-basic-win32.dll"
  Delete "$INSTDIR\bin\pthreadGC.dll"
  Delete "$INSTDIR\bin\libsqlite3-0.dll"
  Delete "$INSTDIR\bin\msvcr70.dll"
  Delete "$INSTDIR\bin\gnunet-gtk.exe"
  Delete "$INSTDIR\bin\gnu.ico"
  
  Delete "$INSTDIR\bin\extract.exe"
  Delete "$INSTDIR\bin\libextractor-0.dll"
  Delete "$INSTDIR\bin\libextractor-1.dll"
  Delete "$INSTDIR\bin\libextractor_asf.dll"
  Delete "$INSTDIR\bin\libextractor_deb.dll"
  Delete "$INSTDIR\bin\libextractor_dvi.dll"
  Delete "$INSTDIR\bin\libextractor_elf.dll"
  Delete "$INSTDIR\bin\libextractor_filename.dll"
  Delete "$INSTDIR\bin\libextractor_gif.dll"
  Delete "$INSTDIR\bin\libextractor_hash_md5.dll"
  Delete "$INSTDIR\bin\libextractor_hash_rmd160.dll"
  Delete "$INSTDIR\bin\libextractor_hash_sha1.dll"
  Delete "$INSTDIR\bin\libextractor_html.dll"
  Delete "$INSTDIR\bin\libextractor_id3v2.dll"
  Delete "$INSTDIR\bin\libextractor_id3v23.dll"
  Delete "$INSTDIR\bin\libextractor_id3v24.dll"
  Delete "$INSTDIR\bin\libextractor_jpeg.dll"
  Delete "$INSTDIR\bin\libextractor_lower.dll"
  Delete "$INSTDIR\bin\libextractor_man.dll"
  Delete "$INSTDIR\bin\libextractor_mime.dll"
  Delete "$INSTDIR\bin\libextractor_mp3.dll"
  Delete "$INSTDIR\bin\libextractor_mpeg.dll"
  Delete "$INSTDIR\bin\libextractor_ole2.dll"
  Delete "$INSTDIR\bin\libextractor_oo.dll"
  Delete "$INSTDIR\bin\libextractor_pdf.dll"
  Delete "$INSTDIR\bin\libextractor_png.dll"
  Delete "$INSTDIR\bin\libextractor_printable_da.dll"
  Delete "$INSTDIR\bin\libextractor_printable_de.dll"
  Delete "$INSTDIR\bin\libextractor_printable_en.dll"
  Delete "$INSTDIR\bin\libextractor_printable_es.dll"
  Delete "$INSTDIR\bin\libextractor_printable_it.dll"
  Delete "$INSTDIR\bin\libextractor_printable_no.dll"
  Delete "$INSTDIR\bin\libextractor_ps.dll"
  Delete "$INSTDIR\bin\libextractor_qt.dll"
  Delete "$INSTDIR\bin\libextractor_real.dll"
  Delete "$INSTDIR\bin\libextractor_riff.dll"
  Delete "$INSTDIR\bin\libextractor_rpm.dll"
  Delete "$INSTDIR\bin\libextractor_split.dll"
  Delete "$INSTDIR\bin\libextractor_tar.dll"
  Delete "$INSTDIR\bin\libextractor_tiff.dll"
  Delete "$INSTDIR\bin\libextractor_util-0.dll"
  Delete "$INSTDIR\bin\libextractor_wav.dll"
  Delete "$INSTDIR\bin\libextractor_zip.dll"

  Delete "$INSTDIR\bin\dht-create.exe"
  Delete "$INSTDIR\bin\dht-fetch.exe"
  Delete "$INSTDIR\bin\dht-insert.exe"
  Delete "$INSTDIR\bin\dht-inserted-drop.exe"
  Delete "$INSTDIR\bin\dht-inserted-list.exe"
  Delete "$INSTDIR\bin\dht-join.exe"
  Delete "$INSTDIR\bin\dht-leave.exe"
  Delete "$INSTDIR\bin\dht-list.exe"
  Delete "$INSTDIR\bin\gnunet-dht-join.exe"
  Delete "$INSTDIR\bin\gnunet-dht-query.exe"
  Delete "$INSTDIR\bin\libgnunetdht_api.dll"
  Delete "$INSTDIR\bin\libgnunetdht_api-0.dll"
  Delete "$INSTDIR\bin\libgnunetdht_datastore_memory-0.dll"
  Delete "$INSTDIR\bin\libgnunetdht_protocol.dll"
  Delete "$INSTDIR\bin\libgnunetrpc_protocol.dll"
  Delete "$INSTDIR\bin\libgnunetrpc_util-0.dll"
  
  Delete "$INSTDIR\etc\pango\pango.aliases"
  Delete "$INSTDIR\etc\pango\pango.modules"
  
  Delete "$INSTDIR\lib\gtk-2.0\2.4.0\engines\libwimp.dll"
  Delete "$INSTDIR\lib\pango\1.4.0\modules\pango-basic-win32.dll"
  Delete "$INSTDIR\share\themes\Default\gtk-2.0\gtkrc.gtkwimp"
  Delete "$INSTDIR\share\themes\Default\gtk-2.0\gtkrc.plain"
  Delete "$INSTDIR\share\themes\Default\gtk-2.0\gtkrc"
  RmDir /REBOOTOK "$INSTDIR\lib\gtk-2.0\2.4.0\engines"
  RmDir /REBOOTOK "$INSTDIR\lib\gtk-2.0\2.4.0"
  RmDir /REBOOTOK "$INSTDIR\lib\gtk-2.0"
  RmDir /REBOOTOK "$INSTDIR\lib\pango\1.4.0\modules"
  RmDir /REBOOTOK "$INSTDIR\lib\pango\1.4.0"
  RmDir /REBOOTOK "$INSTDIR\lib\pango"
  RmDir /REBOOTOK "$INSTDIR\lib"
  RmDir /REBOOTOK "$INSTDIR\share\themes\Default\gtk-2.0"
  RmDir /REBOOTOK "$INSTDIR\share\themes\Default"
  RmDir /REBOOTOK "$INSTDIR\share\themes"
  RmDir /REBOOTOK "$INSTDIR\share"

  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Website.lnk"
  Delete "$DESKTOP\GNUnet.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\GNUnet.lnk"

  ReadEnvStr $USR_PROF "USERPROFILE"
  StrLen $DIRLEN $USR_PROF
  IntCmp $DIRLEN 0 no_profile
  goto rm_conf
 no_profile:
  ReadRegStr $USR_PROF "HKCU" "Software\Microsoft\Windows\CurrentVersion\Explorer\Shell Folders" "Personal"
  StrLen $DIRLEN $USR_PROF
  IntCmp $DIRLEN 0 gn_dir
  goto rm_conf
 gn_dir:
  StrCpy $USR_PROF "$INSTDIR\home"
 rm_conf:
  StrCpy $USR_PROF "$USR_PROF\.gnunet"

  RMDir /REBOOTOK $USR_PROF
  RMDir /REBOOTOK "$SMPROGRAMS\$ICONS_GROUP"
  RMDir /REBOOTOK "$INSTDIR\etc\pango\"
  RMDir /REBOOTOK "$INSTDIR\etc\"
  RMDir /REBOOTOK "$INSTDIR\bin"
  RMDir /REBOOTOK "$INSTDIR\"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
;  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey HKLM "Software\GNU\GNUnet"
  DeleteRegKey HKLM "Software\GNU\libextractor"
  SetAutoClose true
SectionEnd
